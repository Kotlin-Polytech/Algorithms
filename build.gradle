import java.nio.file.Files
import java.util.stream.Collectors

buildscript {
    repositories {
        maven { url 'http://dl.bintray.com/vorpal-research/kotlin-maven' }
    }
    dependencies {
        classpath 'org.jetbrains.research:kfirst-runner-gradle-plugin:19.0.3'
    }
}

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'ru.spbstu'
version '19.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    maven { url 'http://dl.bintray.com/vorpal-research/kotlin-maven' }
    mavenLocal()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile "org.jetbrains.research:kfirst-runner:19.0.2"
    testCompile "org.jetbrains.kotlin:kotlin-test"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit5"
    testCompile "org.junit.jupiter:junit-jupiter-api:5.5.1"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.5.1"
    testRuntime "org.junit.platform:junit-platform-launcher:1.5.1"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xnew-inference"]
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xnew-inference"]
}

compileJava.options.encoding = 'UTF-8'

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

apply plugin: org.jetbrains.research.runner.KFirstRunnerPlugin

task kotoedRun(
        type: org.jetbrains.research.runner.KFirstRunnerTask,
        dependsOn: ['clean', 'testClasses']) {
    def testPackages = sourceSets['test'].java.srcDirs.stream()
            .flatMap {
                dir -> Files.walk(dir.toPath())
                            .map { it.toFile() }
                            .filter { it.isDirectory() }
                            .map { it.path }
                            .map { it.substring(dir.path.length()) }
            }
            .filter { !it.isEmpty() }
            .map { it.substring(1) }
            .map { it.replace('/', '.').replace('\\', '.') }
            .collect(Collectors.toList())
            .sort()
    packages = testPackages
}
